from snakemake.utils import min_version
from pathlib import Path
import shutil
import os

##### minimum snakemake version #####
min_version("8.8.0")

configfile: "config.yaml"

##### resolve paths #####
BASE = Path(config["data_dir"])

SAMPLES_FILE = BASE / config["samples"]

INPUT = {
    k: BASE / config["input"][k]
    for k in ("long_reads", "short_reads")
}

DB = Path(config["input"]["databases"])
REFERENCE_HUMAN = DB / config["reference"]["human"]

##### samples #####
with open(SAMPLES_FILE) as f:
    next(f)
    SAMPLES = [l.split()[0] for l in f if l.strip()]

##### Create symlink on startup to dodge CheckM error #####
SYMLINK = Path("m")
TARGET = BASE / config['output']['assembly']['binning']

TARGET.mkdir(parents=True, exist_ok=True)

if SYMLINK.is_symlink():
    if SYMLINK.resolve() != TARGET.resolve():
        SYMLINK.unlink()
        os.symlink(TARGET, SYMLINK)
elif SYMLINK.exists():
    if SYMLINK.is_dir():
        shutil.rmtree(SYMLINK)
    else:
        SYMLINK.unlink()
    os.symlink(TARGET, SYMLINK)
else:
    os.symlink(TARGET, SYMLINK)

##### entry point #####
rule all:
    input:
        BASE / "hybrid_temp/drep/hybrid/Cdb.csv",
        BASE / "result/metawrap_bins/hybrid/hybrid_genomeInfo.csv",
        expand(SYMLINK / "{sample}_binning/bin_reassembly/reassembly_results.png", sample=SAMPLES),
        expand(SYMLINK / "{sample}_binning/bin_refinement/metawrap_70_10_bins.stats", sample=SAMPLES),
        expand(SYMLINK / "{sample}_binning/concoct_bins/bin.1.fa", sample=SAMPLES),
        expand(SYMLINK / "{sample}_binning/metabat2_bins/bin.1.fa", sample=SAMPLES),
        expand(SYMLINK / "{sample}_binning/maxbin2_bins/bin.1.fa", sample=SAMPLES),
        expand(BASE / config["output"]["assembly"]["spades"] / "{sample}_assembly/contigs.fasta", sample=SAMPLES),
        expand(BASE / config["output"]["qc"]["read_qc"] / "{sample}/final_pure_reads_1.fastq", sample=SAMPLES),
        REFERENCE_HUMAN.with_suffix(".bitmask"),
        REFERENCE_HUMAN.with_suffix(".srprism"),
        expand(BASE / config["output"]["dehost"]["sam"] / "nano.{sample}.dehost.fq.gz", sample=SAMPLES)
        

##### load rules #####

include: "rules/nano_dehost.smk"
include: "rules/bmtagger_index.smk"
include: "rules/read_qc.smk"
include: "rules/assembly.smk"
include: "rules/binning.smk"
include: "rules/bin_refinement.smk"
include: "rules/reassembly.smk"
include: "rules/summarize_bins.smk"
include: "rules/dereplication.smk"
